// Absolute value of an integer.
define absInt (int -> int):
  -> x;
  if (x < 0):
    x negInt
  else:
    x

// Absolute value of a floating-point number.
define absFloat (float -> float):
  -> x;
  if (x <. 0.0):
    x negFloat
  else:
    x

// Whether all elements of a vector satisfy a predicate.
define all<a> ([a] (a -> bool) -> bool):
  map and

// Whether all elements of a vector are true.
define and ([bool] -> bool):
  true \&& foldl

// Whether any element of a vector satisfies a predicate.
define any<a> ([a] (a -> bool) -> bool):
  map or

// Whether a floating-point number is even.
define evenFloat (float -> bool):
  -> x; x %. 2.0 =. 0.0

// Whether an integer is even.
define evenInt (int -> bool):
  -> x; x % 2 = 0

// The maximum of two integers.
define maxInt (int int -> int):
  -> a b;
  if (a > b) {a} else {b}

// The maximum of three integers.
define maxInt3 (int int int -> int):
  maxInt maxInt

// The maximum of a vector of integers.
define maxInts ([int] -> int?):
  \maxInt foldl1

// The minimum of two integers.
define minInt (int int -> int):
  -> a b;
  if (a < b) {a} else {b}

// The minimum of three integers.
define minInt3 (int int int -> int):
  minInt minInt

// The minimum of a vector of integers.
define minInts ([int] -> int?):
  \minInt foldl1

// Floating-point negation.
define negFloat (float -> float):
  kitten::intrinsic::negFloat

// Integer negation.
define negInt (int -> int):
  kitten::intrinsic::negInt

// Logical NOT.
define not (bool -> bool):
  kitten::intrinsic::notBool

// Whether a floating-point number is odd.
define oddFloat (float -> bool):
  evenFloat not

// Whether an integer is odd.
define oddInt (int -> bool):
  evenInt not

// Whether any element of a vector is true.
define or ([bool] -> bool):
  false \|| foldl

// The ratio of a circle's circumference to its diameter.
define pi (-> float): 3.141592653589793

// The product of a vector of floating-point numbers.
define productFloats ([float] -> float):
  1.0 \*. foldl

// The product of a vector of integers.
define productInts ([int] -> int):
  1 \* foldl

// The sum of a vector of floating-point numbers.
define sumFloats ([float] -> float):
  0.0 \+. foldl

// The sum of a vector of integers.
define sumInts ([int] -> int):
  0 \+ foldl

// The ratio of a circle's circumference to its radius.
define tau (-> float): 6.283185307179586

// Integer inequality.
infix 4 !=
define != (int int -> bool):
  kitten::intrinsic::neInt

infix 4 ≠
define ≠ (int int -> bool):
  kitten::intrinsic::neInt

// Floating-point inequality.
infix 4 !=.
define !=. (float float -> bool):
  kitten::intrinsic::neFloat

infix 4 ≠.
define ≠. (float float -> bool):
  kitten::intrinsic::neFloat

// Integer modulus.
infix left 7 %
define % (int int -> int):
  kitten::intrinsic::modInt

// Floating-point modulus.
infix left 7 %.
define %. (float float -> float):
  kitten::intrinsic::modFloat

// Bitwise AND.
infix left 7 &
define & (int int -> int):
  kitten::intrinsic::andInt

// Logical AND.
infix right 3 &&
define && (bool bool -> bool):
  kitten::intrinsic::andBool

infix right 3 ∧
define ∧ (bool bool -> bool):
  kitten::intrinsic::andBool

// Integer multiplication.
infix left 7 *
define * (int int -> int):
  kitten::intrinsic::mulInt

infix left 7 ×
define × (int int -> int):
  kitten::intrinsic::mulInt

// Floating-point multiplication.
infix left 7 *.
define *. (float float -> float):
  kitten::intrinsic::mulFloat

infix left 7 ×.
define ×. (float float -> float):
  kitten::intrinsic::mulFloat

// Floating-point exponentiation
infix right 8 **.
define **. (float float -> float):
  kitten::intrinsic::powFloat

// Integer addition.
infix right 6 +
define + (int int -> int):
  kitten::intrinsic::addInt

// Floating-point addition.
infix left 6 +.
define +. (float float -> float):
  kitten::intrinsic::addFloat

// Integer subtraction.
infix left 6 -
define - (int int -> int):
  kitten::intrinsic::subInt

infix left 6 −
define − (int int -> int):
  kitten::intrinsic::subInt

// Floating-point subtraction.
infix left 6 -.
define -. (float float -> float):
  kitten::intrinsic::subFloat

infix left 6 −.
define −. (float float -> float):
  kitten::intrinsic::subFloat

// Integer division.
infix left 7 /
define / (int int -> int):
  kitten::intrinsic::divInt

infix left 7 ÷
define ÷ (int int -> int):
  kitten::intrinsic::divInt

// Floating-point division.
infix left 7 /.
define /. (float float -> float):
  kitten::intrinsic::divFloat

infix left 7 ÷.
define ÷. (float float -> float):
  kitten::intrinsic::divFloat

// Integer less than.
infix 4 <
define < (int int -> bool):
  kitten::intrinsic::ltInt

// Floating-point less than.
infix 4 <.
define <. (float float -> bool):
  kitten::intrinsic::ltFloat

// Integer less than or equal.
infix 4 <=
define <= (int int -> bool):
  kitten::intrinsic::leInt

infix 4 ≤
define ≤ (int int -> bool):
  kitten::intrinsic::leInt

// Floating-point less than or equal.
infix 4 <=.
define <=. (float float -> bool):
  kitten::intrinsic::leFloat

infix 4 ≤.
define ≤. (float float -> bool):
  kitten::intrinsic::leFloat

// Integer equality.
infix 4 =
define = (int int -> bool):
  kitten::intrinsic::eqInt

// Floating-point equality.
infix 4 =.
define =. (float float -> bool):
  kitten::intrinsic::eqFloat

// Integer greater than.
infix 4 >
define > (int int -> bool):
  kitten::intrinsic::gtInt

// Floating-point greater than.
infix 4 >.
define >. (float float -> bool):
  kitten::intrinsic::gtFloat

// Integer greater than or equal.
infix 4 >=
define >= (int int -> bool):
  kitten::intrinsic::geInt

infix 4 ≥
define ≥ (int int -> bool):
  kitten::intrinsic::geInt

// Floating-point greater than or equal.
infix 4 >=.
define >=. (float float -> bool):
  kitten::intrinsic::geFloat

infix 4 ≥.
define ≥. (float float -> bool):
  kitten::intrinsic::geFloat

// Bitwise XOR.
infix left 6 ^
define ^ (int int -> int):
  kitten::intrinsic::xorInt

// Boolean XOR.
infix right 2 ^^
define ^^ (bool bool -> bool):
  kitten::intrinsic::xorBool

infix right 2 ⊻
define ⊻ (bool bool -> bool):
  kitten::intrinsic::xorBool

// Bitwise OR.
infix left 5 |
define | (int int -> int):
  kitten::intrinsic::orInt

// Logical OR.
infix right 2 ||
define || (bool bool -> bool):
  kitten::intrinsic::orBool

infix right 2 ∨
define ∨ (bool bool -> bool):
  kitten::intrinsic::orBool

// Bitwise NOT.
define ~ (int -> int):
  kitten::intrinsic::notInt
