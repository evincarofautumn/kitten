data Stream @ a:
  case EmptyStream;
  case StreamValue a (-> Stream@a);

// Operator alias for 'StreamValue'.
infix_right 5 +>
define +> @ a (a (-> Stream@a) -> Stream@a):
  StreamValue

// Concatenates two streams. If the first stream is infinite, the result
// is the first stream.
define catStreams @ a (Stream@a Stream@a -> Stream@a):
  -> xs ys;
  match (xs):
  case EmptyStream: ys
  case StreamValue -> x rest:
    x +>: rest apply ys catStreams

// Concatenates a stream of streams. If the first stream is infinite,
// the result is the first stream.
define concatStreams @ a (Stream@Stream@a -> Stream@a):
  match:
  case EmptyStream: EmptyStream
  case StreamValue -> streams:
    match:
    case EmptyStream: streams apply concatStreams
    case StreamValue -> value values:
      value +>: (values apply +> streams) concatStreams

// Keeps a stream of the first n elements of a stream, tossing the rest.
define keepStream @ a (Stream@a int -> Stream@a):
  -> n;
  if (n <= 0): drop EmptyStream
  else:
    match:
    case EmptyStream: EmptyStream
    case StreamValue -> x xs:
      x +>: xs apply (n - 1) keepStream

// Lifts a function on values to a function on streams.
define mapStream @ a b (Stream@a (a -> b) -> Stream@b):
  -> f;
  match:
  case EmptyStream: EmptyStream
  case StreamValue -> x xs:
    x f apply +>: xs apply f mapStream

// Repeats a stream infinitely.
define streamCycle @ a (Stream@a -> Stream@a):
  streamRepeat concatStreams

// Generates a stream counting up from a starting integer.
define streamFrom (int -> Stream@int):
  -> x; x +>: (x + 1) streamFrom

// Whether a stream is empty.
define streamIsEmpty @ a (Stream@a -> bool):
  match:
  case EmptyStream: true
  default: false

// Repeats a value infinitely.
define streamRepeat @ a (a -> Stream@a):
  -> x; x +>: x streamRepeat

// Converts a finite stream to a vector.
define streamToVector @ a (Stream@a -> [a]):
  match:
  case EmptyStream: []
  case StreamValue -> x xs:
    xs apply streamToVector
    x prepend

// Tosses the first n elements of a stream.
define tossStream @ a (Stream@a int -> Stream@a):
  -> n;
  if (n > 0):
    match:
    case EmptyStream: EmptyStream
    case StreamValue -> _ xs:
      xs apply (n - 1) tossStream

// Converts vector to a finite stream.
define vectorToStream @ a ([a] -> Stream@a):
  -> xs;
  option (xs head) -> x: x +>: xs tail vectorToStream
  else: EmptyStream
