// Gets the first element of a tuple or pair.
define first<a, b> (a & b -> a):
  kitten::intrinsic::first

// Gets the second element of a tuple.
define second<a, b, c> (a & b & c -> b):
  rest first

// Gets the third element of a tuple.
define third<a, b, c, d> (a & b & c & d -> c):
  rest rest first

// Flips the elements of a pair.
define flip<a, b> (a & b -> b & a):
  unpair swap pair

// Gets the fourth element of a tuple.
define fourth<a, b, c, d, e> (a & b & c & d & e -> d):
  rest rest rest first

// Constructs a pair.
define pair<a, b> (a b -> a & b):
  kitten::intrinsic::pair

// Constructs a triple.
define triple<a, b, c> (a b c -> a & b & c):
  pair pair

// Gets the second element of a pair or the tail of a tuple.
define rest<a, b> (a & b -> b):
  kitten::intrinsic::rest

// Extracts a pair.
define unpair<a, b> (a & b -> a b):
  \first \rest bothTo

// Extracts a triple.
define untriple<a, b, c> (a & b & c -> a b c):
  unpair unpair
