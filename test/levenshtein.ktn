define levenshtein ([char] [char] -> int):
  -> first second;
  first length -> rows;
  second length -> columns;

  // A matrix such that the value at row i, column j is the
  // Levenshtein distance between the first i characters of
  // the first string and the first j characters of the
  // second string.
  0 (columns + 1) replicate (rows + 1) replicate

  // Distance from any first string to empty second string.
  from (0) to (rows) -> row:
    (row, 0) !!= row

  // Distance from any second string to empty first string.
  from (0) to (columns) -> column:
    (0, column) !!= column

  from (1) to (rows) -> row:
    from (1) to (columns) -> distances column:
      first ! (row - 1) -> firstChar;
      second ! (column - 1) -> secondChar;

      distances
      if (firstChar secondChar eqChar):
        distances !! (row - 1, column - 1)
      else:
        distances !! (row - 1, column);      // deletion
        distances !! (row,     column - 1);  // insertion
        distances !! (row - 1, column - 1);  // substitution
        minInt3 (+ 1)
      ((row, column) !!=)

  (!! (rows, columns))
 
"kitten" "sitting" levenshtein sayInt
"rosettacode" "raisethysword" levenshtein sayInt
