/*
 * tictactoe.ktn
 *
 * Contributed by @strager <https://github.com/strager/>
 *
 * Modified by Jon Purdy <https://github.com/evincarofautumn/>
 *
 */

ticTacToe

def ticTacToe (->):
  { game playAgain } loopWhile

def game (->):
  newBoard 'x' gameLoop -> finalBoard winner;

  newline
  if (winner '*' eqChar):
    "It's a tie!\n"
  else:
    [winner] " wins!\n" cat
  print

  finalBoard printBoard

def gameLoop ([char] char -> [char] char):
  -> board player;

  board getWinner -> winner;
  if ("xo*" winner elemChar):
    board winner
  else:
    board player askTurn
    player nextPlayer gameLoop

def askTurn ([char] char -> [char]):
  -> board player;

  board printBoard

  [
    "It is ",
    [player],
    "'s turn. Choose your move: ",
  ] concat print

  getLine -> input;
  input readMoveLine -> mxy;

  option (mxy):
    -> xy;
    board
    xy first xy rest xyToCellNumber
    player editBoard
  else:
    if (input "q" eqString):
      0 exit
    else:
      "I didn't understand that. Type e.g. \"a3\" to "
      "move in the bottom-left cell. Type \"q\" to exit."
      cat say
      board player askTurn

def nextPlayer (char -> char):
  -> p;
  if (p 'x' eqChar) {'o'} else {'x'}

def checkSets (-> [[int]]):
  [
    [0, 1, 2], [3, 4, 5], [6, 7, 8],  // Rows
    [0, 3, 6], [1, 4, 7], [2, 5, 8],  // Columns
    [0, 4, 8], [2, 4, 6],             // Diagonals
  ]

// ' ', 'x', or 'o'
def getCheckSetWinner ([char] [int] -> char):
  getAll -> players;
  if (players { {'x' eqChar} {false} option___else_ } all):
    'x'
  else:
    if (players { {'o' eqChar} {false} option___else_ } all):
      'o'
    else:
      ' '

// ' ', 'x', 'o', or '*'
def getWinner ([char] -> char):
  -> board;

  board
    { ' ' eqChar } filter
    isEmpty
    -> filled;

  if (filled): '*'
  else:
    checkSets
    { board swap getCheckSetWinner } map
    { ' ' neChar } filter
    -> winners;

    if (winners isEmpty):
      ' '
    else:
      if (winners { 'x' eqChar } all):
        'x'
      else:
        if (winners { 'o' eqChar } all):
          'o'
        else:
          '*'

def playAgain (-> bool):
  "Play another game? [yN] " prompt -> input;
  if (input isYes):
    newline true
  else:
    if (input isNo || input isEmpty):
      newline false
    else:
      playAgain

def isYes ([char] -> bool): { "yY" swap elemChar } any
def isNo ([char] -> bool): { "nN" swap elemChar } any

// Board

def newBoard (-> [char]): ' ' 9 replicate

def editBoard ([char] int char -> [char]):
  -> number player;
  number cellNumberToIndex @= player

def printBoard ([char] ->):
  showBoard print

def showBoard ([char] -> [char]):
  -> board;
  "  --+-+--\n" -> divider;
  "   A B C\n" -> heading;
  [
    heading, "1 ", board 0 showBoardRow, "\n",
    divider, "2 ", board 1 showBoardRow, "\n",
    divider, "3 ", board 2 showBoardRow, "\n",
  ] concat

def showBoardRow ([char] int -> [char]):
  -> board rowIndex;
  rowIndex * 3 -> index;
  [
    ' ', board (0 + index) showBoardCellAt,
    '|', board (1 + index) showBoardCellAt,
    '|', board (2 + index) showBoardCellAt,
  ]

def showBoardCellAt ([char] int -> char):
  cellNumberToIndex (@!) showBoardCell

def cellNumberToIndex (int -> int):
  (8 -)

def showBoardCell (char -> char) {}

// Input

def readMoveLine ([char] -> (int & int)?):
  -> chars;
  if (chars length = 2):
    (chars @! 0) (chars @! 1) readXY
  else:
    none

def readXY (char char -> (int & int)?):
  -> x y;
  x y readXY_ -> mxy;
  option (mxy):
    some
  else:
    y x readXY_

def readXY_ (char char -> (int & int)?):
  \readX \readY both -> mx my;
  mx my \pair liftOption2

def readX (char -> int?):
  -> c;
  if ("aA" c elemChar): 0 some
  else:
    if ("bB" c elemChar): 1 some
    else:
      if ("cC" c elemChar): 2 some
      else: none

def readY (char -> int?):
  -> c;
  if ("1!" c elemChar): 0 some
  else:
    if ("2@" c elemChar): 1 some
    else:
      if ("3#" c elemChar): 2 some
      else: none

def xyToCellNumber (int int -> int):
  -> x y;
  y * 3 + x
